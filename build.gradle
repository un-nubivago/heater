plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "heater" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

fabricApi {
    configureDataGeneration {
        outputDirectory = file("src/generated/resources")
        client = true
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:burning:${project.burning_version}"
}

processResources {
    inputs.property "mod_version", project.mod_version
    inputs.property "loader_version", project.loader_version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "fabric_version", project.fabric_version
    inputs.property "burning_version", project.burning_version

    filesMatching("fabric.mod.json") {
         expand([
            'mod_version': project.mod_version,
            'loader_version': project.loader_version,
            'minecraft_version': project.minecraft_version,
            'fabric_version': project.fabric_version,
            'burning_version': project.burning_version
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project.project_id}"
    versionName = "Heater ${project.mod_version}"
    changelog = "sh CHANGELOG.sh".execute().text
    versionType = "release"
    gameVersions = Arrays.asList("${project.game_versions}".toString().split(","))
    uploadFile = remapJar
    syncBodyFrom = "sh README.sh".execute().text
    dependencies {
        required.project "fabric-api"
        required.project "burning"
    }
    // debugMode = true
}
